plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

group "xyz.oribuin"
version "1.1.4-pre2"
sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

compileJava {
    options.compilerArgs += ['-parameters']
    options.encoding = "UTF-8"
}

repositories {
    mavenCentral()
    mavenLocal()

    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://repo.rosewooddev.io/repository/public/" }
    maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url "https://repo.codemc.org/repository/maven-public" }
    maven { url "https://jitpack.io" }
    maven { url "https://repo.mattstudios.me/artifactory/public/" }
}

dependencies {
    compileOnly "org.spigotmc:spigot:1.17"
    compileOnly "me.clip:placeholderapi:2.10.9"
    compileOnly "org.jetbrains:annotations:23.0.0"
    compileOnly "com.github.oraxen:oraxen:afc4903680"
    compileOnly 'net.kyori:adventure-api:4.11.0'

    implementation "dev.rosewood:rosegarden:1.1.0.42-SNAPSHOT"
    implementation ("dev.triumphteam:triumph-gui:3.1.2") {  // https://mf.mattstudios.me/triumph-gui/introduction
        exclude group: "com.google.code.gson", module: "gson" // Remove GSON, Already included in spigot api
        exclude group: "net.kyori", module: "*" // Remove kyori, takes up too much space
    }
}


shadowJar {
    archiveClassifier.set(null)
    relocate("dev.rosewood.rosegarden", "${project.group}.eternaltags.libs.rosegarden")
    relocate("dev.triumphteam.gui", "${project.group}.eternaltags.libs.gui")
}

tasks.getByName("build").dependsOn("shadowJar")